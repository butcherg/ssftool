# Generates a plot of two SSF datasets for comparison. First dataset is plotted
# with bold lines, second dataset is plotted with thin lines.
# Usage: $ gnuplot -c ssfplot dataset1.csv dataset2.csv "title"
# Output: ssfcomp.png

set datafile separator ","
set terminal png size 640,480 
set output 'ssfcomp.png'
set title ARG3
unset key
set xlabel "wavelength (nm)"
set linetype 1 lw 2 lc rgb "red"
set linetype 2 lw 2 lc rgb "green"
set linetype 3 lw 2 lc rgb "blue"
set linetype 4 lw 1 lc rgb "red"
set linetype 5 lw 1 lc rgb "green"
set linetype 6 lw 1 lc rgb "blue"

plot ARG1 using 1:2 with lines, "" using 1:3 with lines, "" using 1:4 with lines, \
ARG2 using 1:2 with lines, "" using 1:3 with lines, "" using 1:4 with lines

